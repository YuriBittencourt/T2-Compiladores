PROGRAM : STATEMENT | FUNCLIST | epsilon
FUNCLIST : FUNCDEF FUNCLIST | FUNCDEF
FUNCDEF : def IDENT (PARAMLIST) {STATELIST}
PARAMLIST : TYPES IDENT PARAMLISTITER | epsilon
PARAMLISTITER : , TYPES IDENT PARAMLISTITER | epsilon
STATEMENT : VARDECL; | ATRIBSTAT; | PRINTSTAT; | READSTAT; | RETURNSTAT; | IFSTAT; | FORSTAT; | {STATELIST} | break; | ;
VARDECL : TYPES IDENT INT
TYPES : int | float | string
INT : [INT_CONSTANT]INT | epsilon
ATRIBSTAT : LVALUE = EXPRESSION | LVALUE = ALLOCEXPRESSION | LVALUE = FUNCCALL
FUNCCALL : IDENT(PARAMLISTCALL)
PARAMLISTCALL : IDENT,PARAMLISTCALL | epsilon
PRINTSTAT : print EXPRESSION
READSTAT : read LVALUE
RETURNSTAT : return
IFSTAT : if( EXPRESSION ) STATEMENT ELSE
ELSE : else STATEMENT | epsilon
FORSTAT : for(ATRIBSTAT; EXPRESSION; ATRIBSTAT) STATEMENT
STATELIST : STATEMENT | STATEMENT STATELIST
ALLOCEXPRESSION : new TYPES EXPRESSIONS
EXPRESSIONS : [EXPRESSION]EXPRESSIONS | [EXPRESSION]
EXPRESSION : NUMEXPRESSION |  NUMEXPRESSION BINARYOPERATOR
BINARYOPERATOR : RELATIONALOPERATOR NUMEXPRESSION | epsilon
RELATIONALOPERATOR : < | > | <= | >= | == | !=
NUMEXPRESSION : TERM SIGNEDTERMS
SIGNEDTERMS : SIGNAL TERM SIGNEDTERMS | epsilon
SIGNAL : + | -
TERM : UNARYEXPR UNARYITER
UNARYITER : UNARYOP UNARYEXPR UNARYITER | epsilon
UNARYOP : * | \ | %
UNARYEXPR : SIGNAL FACTOR | FACTOR
FACTOR : INT_CONSTANT | FLOAT_CONSTANT | STRING_CONSTANT | null | LVALUE | (EXPRESSION)
LVALUE : IDENT | IDENT EXPRESSIONS

# regexes
FLOAT_CONSTANT: ^[0-9]+\.[0-9]+?$
INT_CONSTANT: ^\d+$
STRING_CONSTANT: ^\"(.)*\"$
IDENT: ^[a-z_]([A-Za-z_0-9])*$
